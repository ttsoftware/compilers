%{ 
   (* See the Moscow ML manual for the syntax and structure. Roughly:

    `%``{`
         header (* with ML-like comments *)
    `%``}`
        declarations  /* with c-like comments */
    `%``%`
        rules         /* with c-like comments */
    `%``%`
        trailer (* with ML-like comments *)
    EOF

    The (optional) header and trailer contain ML code to include in the
    generated file (after the data declaration and at the end).

    Compiling this file with:
        $ mosmlyac -v G_AB.grm
    will generate the code in "G_AB.sig" and "G_AB.sml" files, and a
    file "G_AB.output" which describes the generated LR(0) automaton.
    *)

    type pos = int*int (* position in string *)
    (* Unfortunately, we cannot use this type in the declarations -
       the code ends up _after_ the data declaration. *)

    (* parse exception *)
    exception ParseError of string
%}

/* Token type definitions (will often be used in the Lexer) */

/* Tokens use position attribute for demonstration (see below for Lexer)
 * As mentioned, the SML code above ends up _after_ this data declaration,
 * so we cannot use any types defined _above_ at this point of the file.
 */
/* tokens skal være sml-typer
   gennem reglerne skal de omdannes til et absyn syntakstræ*/

%token <AbSyn.Pos> TProgram TFunction TProcedure TVar TBegin TEnd TIf TThen TElse TWhile TDo TReturn TArray TOf TInt TBool TChar TAnd TOr TNot TAssign TPlus TMinus TTimes TSlash TEq TLess TLParen TRParen TLBracket TRBracket TLCurly TRCurly TComma TSemi TColon TEOF
%token <bool*AbSyn.Pos> TBLit
%token <int*AbSyn.Pos> TNLit
%token <char*AbSyn.Pos> TCLit
%token <string*AbSyn.Pos> TSLit TId

%nonassoc LowPrec
%nonassoc TElse
%right TOr
%right TAnd
%nonassoc TNot
%nonassoc TEq TLess
%left TPlus TMinus
%left TTimes TSlash

/* start symbol */
%start Program

/* types returned by rules below */
%type <AbSyn.Prog> Program
%type <AbSyn.Prog> Prog
%type <AbSyn.Prog> FunDecs
%type <AbSyn.FunDec> FunDec
%type <AbSyn.StmtBlock> Block 
%type <AbSyn.Stmt list> SBlock
%type <AbSyn.Stmt list> StmtSeq
%type <AbSyn.Stmt> Stmt
%type <AbSyn.LVAL> LVal
%type <AbSyn.Exp option> Ret
%type <AbSyn.Exp> Exp
%type <AbSyn.Dec list> PDecl Params
%type <AbSyn.Dec> Dec
%type <AbSyn.Dec list> Decs
%type <AbSyn.Type> Type
%type <AbSyn.Exp list> CallParams Exps

%%

/* rules - a separate start rule is added automatically */
/* OBS: alle T typerne er tupler!! kan man #1 på dem?? mht TId
   spørgsmål: hvad gør man med pos?
   hvornår skal teof bruges?
   if then udtryk under statement findes ikke (er stmt nullable?)
   er operator tingen overhovedet mulig?
*/

/* PROGRAM STRUCTURE*/
Program: Prog TEOF                             { $1 } 
;
Prog : 
    TProgram TId TSemi FunDecs                 { $4 }
;
FunDecs :
    FunDecs FunDec                             { $1 @ [$2] }
  | FunDec                                     { [$1] }
;
FunDec :
    TFunction TId TLParen PDecl TRParen TColon Type Block TSemi
        { AbSyn.Func($7, #1 $2, $4, $8, $1) }
  | TProcedure TId TLParen PDecl TRParen Block TSemi
        { AbSyn.Proc(#1 $2, $4, $6, $1) }
;
Block :
    SBlock              { AbSyn.Block ([], $1) }
  | TVar Decs SBlock    { AbSyn.Block ($2, $3) }
;
SBlock :
    TBegin StmtSeq TSemi TEnd                  { $2 }
  | Stmt                                       { [$1] }
;
StmtSeq :
    StmtSeq TSemi Stmt                         { $1 @ [$3] }
  | Stmt                                       { [$1] }
;

/* STATEMENTS */
Stmt :
    TId TLParen CallParams TRParen         { AbSyn.ProcCall (#1 $1, $3, #2 $1) }
  | TIf Exp TThen Block TElse Block            { AbSyn.IfThEl ($2, $4, $6, $1) }
  | TIf Exp TThen Block %prec LowPrec { AbSyn.IfThEl ($2, $4, AbSyn.Block([],[]), $1)}
  /* prec gives precedence as LowPrec */
  | TWhile Exp TDo Block                       { AbSyn.While ($2, $4, $1) }
  | TReturn Ret                                { AbSyn.Return ($2, $1) }
  | LVal TAssign Exp                        { AbSyn.Assign ($1, $3, $2) }
;

/* L-VALUES AND EXPRESSIONS */
LVal :
    TId                                        { AbSyn.Var (#1 $1) }
  | TId TLBracket Exps TRBracket               { AbSyn.Index (#1 $1, $3) }
;
Ret :
    Exp                                        { SOME $1 }
  |                                            { NONE }
;
Exp :
    TNLit               { AbSyn.Literal (AbSyn.BVal(AbSyn.Num(#1 $1)), #2 $1) }
  | TBLit               { AbSyn.Literal (AbSyn.BVal(AbSyn.Log(#1 $1)), #2 $1) }
  | TCLit               { AbSyn.Literal (AbSyn.BVal(AbSyn.Chr(#1 $1)), #2 $1) } 
  | TSLit                         { AbSyn.StrLit  (#1 $1, #2 $1) }
  | TLCurly Exps TRCurly                       { AbSyn.ArrLit  ($2,$1) }
  | LVal                                       { AbSyn.LValue  ($1, (0,0)) }
  | TNot Exp                                   { AbSyn.Not     ($2, $1) }
  | Exp TPlus Exp       { AbSyn.Plus ($1, $3, $2) }
  | Exp TMinus Exp      { AbSyn.Minus ($1, $3, $2) }
  | Exp TTimes Exp      { AbSyn.Times ($1, $3, $2) }
  | Exp TSlash Exp      { AbSyn.Div ($1, $3, $2) }
  | Exp TEq Exp         { AbSyn.Equal ($1, $3, $2) }
  | Exp TLess Exp       { AbSyn.Less ($1, $3, $2) }
  | Exp TAnd Exp        { AbSyn.And ($1, $3, $2) }
  | Exp TOr Exp         { AbSyn.Or ($1, $3, $2) }
  | TLParen Exp TRParen                        { $2 }
  | TId TLParen CallParams TRParen           { AbSyn.FunApp (#1 $1, $3, # 2$1) }
;

/* VARIABLE AND PARAMETER DECLARATIONS, TYPES */
PDecl : 
    Params                                     { $1 }
  |                                            { [] }
;
Params :
    Params TSemi Dec                           { $1 @ [$3] }
  | Dec                                        { [$1] }
;
Dec :
    TId TColon Type                            { AbSyn.Dec (#1 $1, $3, #2 $1) }
;
Decs :
    Decs Dec TSemi                             { $1 @ [$2] }
  | Dec TSemi                                  { [$1] }
;
Type :
    TInt                                       { AbSyn.Int ($1) }
  | TChar                                      { AbSyn.Char ($1) }
  | TBool                                      { AbSyn.Bool ($1) }
  | TArray TOf Type                            { AbSyn.Array ($3,$1) }
;

/* FUNCTION AND PROCEDURE PARAMETERS AND INDEX LISTS */
CallParams :
    Exps                                       { $1 }
  |                                            { [] }
;
Exps :
    Exp TComma Exps                            { $1 :: $3 }
  | Exp                                        { [$1] }

%%

(* SML trailer

 At this point we can use the parse function (%start above), whose type is
   Start : (Lexing.lexbuf -> token) -> Lexing.lexbuf -> Exp;

  (Lexing.lexbuf -> token) is usually mosmllex-generated, but a simple hack
  here.

*)